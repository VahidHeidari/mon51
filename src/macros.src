;********************************************************************
;                                                                   *
; MACROS.SRC                                                        *
;                                                                   *
;********************************************************************


;********************************************************************
; JLT - Jump to "label" if accumulator Less Than "value"            *
;********************************************************************
%*define(jlt(value,label))
        (cjne   a,#%value,$+3             ;JLT
        jc      %label)


;********************************************************************
; JGT - Jump to "label" if accumulator Greater Than "value"         *
;********************************************************************
%*define(jgt(value,label))
        (cjne   a,#%value+1,$+3           ;JGT
        jnc     %label)


;********************************************************************
; JLE - Jump to "label" if accumulator Less than or Equal to "value"*
;********************************************************************
%*define(jle(value,label))
        (cjne   a,#%value+1,$+3           ;JLE
        jc      %label)


;********************************************************************
; JGE - Jump to "label" if accumulator Greater than or Equal to     *
;       "value"                                                     *
;********************************************************************
%*define(jge(value,label))
        (cjne   a,#%value,$+3             ;JGE
        jnc     %label)


;********************************************************************
; JOR - Jump to "label" if accumulator Out of Range of "lower_value"*
;       and "upper_value"                                           *
;********************************************************************
%*define(jor(lower_value,upper_value,label))
        (cjne   a,#%lower_value,$+3       ;JOR
        jc      %label
        cjne    a,#%upper_value+1,$+3
        jnc     %label)


;********************************************************************
; JIR - Jump to "label" if accumulator In Range of "lower_value" and*
;       "upper_value"                                               *
;********************************************************************
%*define(jir(lower_value,upper_value,label)) local skip
        (cjne   a,#%lower_value,$+3       ;JIR
        jc      %skip
        cjne    a,#%upper_value+1,$+3
        jc      %label
%skip:  )

;********************************************************************
; DECREMENT DPTR                                                    *
;********************************************************************
%*define(dec    dptr) local skip
        (push   acc             ;DEC_DPTR
        dec     dpl
        mov     a,dpl
        cjne    a,#0FFH,%skip
        dec     dph
%skip:  pop     acc)

;********************************************************************
; PUSH DPTR ONTO STACK                                              *
;********************************************************************
%*define(push   dptr)
        (push   dpl             ;PUSH_DPTR
        push    dph)


;********************************************************************
; POP DPTR FROM STACK                                               *
;********************************************************************
%*define(pop    dptr)
        (pop    dph             ;POP_DPTR
        pop     dpl)

;********************************************************************
%*define (enable_register_bank(n))
        (%if (%n) then (setb   rs0) else (clr    rs0) fi
       %if (%n gt 1) then (setb   rs1) else (clr    rs1) fi)

;********************************************************************
%*define (send_message(p))
        (push   dpl
        push    dph
        mov    dptr,#%p
        call   outstr
        pop    dph
        pop    dpl)

